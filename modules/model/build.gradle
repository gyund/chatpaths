plugins {
	id 'com.android.library'
	id 'kotlin-android'
	id 'kotlin-kapt'
	id 'dagger.hilt.android.plugin'
//	id 'com.google.devtools.ksp'
}

apply from: "$project.rootDir/common.gradle"

android {
	compileSdk 34

	defaultConfig {
		minSdkVersion 27
		targetSdkVersion 34

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		consumerProguardFiles "consumer-rules.pro"

		javaCompileOptions {
			annotationProcessorOptions {
				arguments += [
						"room.schemaLocation":"$projectDir/schemas".toString(),
						"room.incremental":"true",
						"room.expandProjection":"true"]
			}
		}

		sourceSets {
			// Adds exported schema location as test app assets.
			androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
		}

	}

	testOptions {
		execution 'ANDROIDX_TEST_ORCHESTRATOR'
	}
	buildFeatures {
		buildConfig = true
	}

	buildTypes {
		debug {
			testCoverageEnabled true
		}
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
//            consumerProguardFiles 'proguard-rules.pro'
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
	}
	namespace 'com.gy.chatpaths.aac.data'
	flavorDimensions 'google'
	productFlavors {
		WithoutFirebase {
			getIsDefault().set(true) // that does the magic
			dimension 'google'
		}
		WithFirebase {
			dimension 'google'
		}
	}
	lint {
		htmlReport true
		lintConfig file("$rootDir/lint.xml")
		xmlReport true
	}
}

dependencies {
	implementation fileTree(dir: "libs", include: ["*.jar"])
	implementation libs.core.ktx
	implementation libs.androidx.appcompat
	implementation libs.androidx.preference.ktx
	implementation platform(libs.firebase.bom)
	implementation libs.firebase.crashlytics
	testImplementation libs.androidx.truth
	testImplementation libs.junit
	androidTestImplementation libs.truth
	androidTestImplementation libs.truth.java8.extension
	androidTestImplementation libs.junit
	androidTestImplementation libs.androidx.espresso.core
	androidTestImplementation libs.androidx.runner
	androidTestUtil libs.androidx.orchestrator

	// Room Dependencies
	implementation libs.androidx.room.runtime
	kapt libs.androidx.room.compiler
	// For Kotlin use kapt instead of annotationProcessor
	implementation libs.androidx.room.ktx
	androidTestImplementation libs.androidx.room.testing

	implementation libs.hilt.android
	kapt libs.hilt.compiler


	// For instrumentation tests
	androidTestImplementation libs.hilt.android.testing
	kaptAndroidTest libs.hilt.compiler

	// For local unit tests
	testImplementation libs.hilt.android.testing
	kaptTest libs.hilt.compiler

	implementation libs.core.ktx
	implementation libs.kotlin.stdlib.jdk7

}
repositories {
	mavenCentral()
}
